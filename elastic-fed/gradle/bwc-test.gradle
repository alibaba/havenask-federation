/*
*Copyright (c) 2021, Alibaba Group;
*Licensed under the Apache License, Version 2.0 (the "License");
*you may not use this file except in compliance with the License.
*You may obtain a copy of the License at

*   http://www.apache.org/licenses/LICENSE-2.0

*Unless required by applicable law or agreed to in writing, software
*distributed under the License is distributed on an "AS IS" BASIS,
*WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*See the License for the specific language governing permissions and
*limitations under the License.
*
* Modifications Copyright Havenask Contributors. See
* GitHub history for details.
*/

import org.havenask.gradle.Version
import org.havenask.gradle.info.BuildParams

ext.bwcTaskName = { Version version ->
  return "v${version}#bwcTest"
}

def bwcTestSnapshots = tasks.register("bwcTestSnapshots") {
  if (project.bwc_tests_enabled) {
    dependsOn tasks.matching { task -> BuildParams.bwcVersions.unreleased.any { version -> bwcTaskName(version) == task.name } }
  }
}

tasks.register("bwcTest") {
  description = 'Runs backwards compatibility tests.'
  group = 'verification'

  if (project.bwc_tests_enabled) {
    dependsOn tasks.matching { it.name ==~ /v[0-9\.]+#bwcTest/ }
  }
}

tasks.withType(Test).configureEach {
  onlyIf { project.bwc_tests_enabled }
}

tasks.named("check").configure {
  dependsOn(bwcTestSnapshots)
}

tasks.findByName("test")?.enabled = false
