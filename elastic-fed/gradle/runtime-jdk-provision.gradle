/* 
*Copyright (c) 2021, Alibaba Group;
*Licensed under the Apache License, Version 2.0 (the "License");
*you may not use this file except in compliance with the License.
*You may obtain a copy of the License at

*   http://www.apache.org/licenses/LICENSE-2.0

*Unless required by applicable law or agreed to in writing, software
*distributed under the License is distributed on an "AS IS" BASIS,
*WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
*See the License for the specific language governing permissions and
*limitations under the License.
*
* Modifications Copyright Havenask Contributors. See
* GitHub history for details.
*/

import org.havenask.gradle.Architecture
import org.havenask.gradle.OS
import org.havenask.gradle.VersionProperties
import org.havenask.gradle.info.BuildParams

apply plugin: 'havenask.jdk-download'

if (BuildParams.getIsRuntimeJavaHomeSet()) {
  configure(allprojects - project(':build-tools')) {
    project.tasks.withType(Test).configureEach { Test test ->
      if (BuildParams.getIsRuntimeJavaHomeSet()) {
        test.executable = "${BuildParams.runtimeJavaHome}/bin/java"
      }
    }
  }
} else {
  jdks {
    provisioned_runtime {
      vendor = VersionProperties.bundledJdkVendor
      version = VersionProperties.getBundledJdk(OS.current().name().toLowerCase())
      platform = OS.current().name().toLowerCase()
      architecture = Architecture.current().name().toLowerCase()
    }
  }

  configure(allprojects - project(':build-tools')) {
    project.tasks.withType(Test).configureEach { Test test ->
      test.dependsOn(rootProject.jdks.provisioned_runtime)
      test.executable = rootProject.jdks.provisioned_runtime.getBinJavaPath()
    }
  }
}
// if neither condition above is executed, tests will use the gradle jvm
